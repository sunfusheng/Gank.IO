apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'

Properties prop = new Properties()
prop.load(new FileInputStream(file("../signing.properties")))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.sunfusheng.gank"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 2
        versionName "1.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        release {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "isDebug", "false"
            signingConfig signingConfigs.release // apk签名
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }

        debug {
            buildConfigField "boolean", "isDebug", "true"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }
    }
}

//生成自定义App名称
def generateAppName(variant) {
    variant.outputs.each { output ->
        def file = output.outputFile
        output.outputFile = new File(file.parent, "Gank.IO-V" + android.defaultConfig.versionName + ".apk")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.orhanobut:logger:1.4'

    // OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.5.0'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // RxDownload
    compile 'zlc.season:rxdownload2:1.1.1'

    // RxPermissions
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'

    // RxLifecycle
    compile 'com.trello:rxlifecycle:1.0'
    compile 'com.trello:rxlifecycle-components:1.0'

    // RxBinding
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.0'

    // PhotoView
    compile 'com.github.chrisbanes:PhotoView:1.3.1'

    // RecyclerView Animators
    compile 'jp.wasabeef:recyclerview-animators:2.2.5'

    // Material Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.2.3'
}
