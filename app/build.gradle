apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

Properties signingProperties = new Properties()
signingProperties.load(new FileInputStream(file("../signings/signing.properties")))

android {
    compileSdkVersion gradle.compileSdkVersion
    buildToolsVersion gradle.buildToolsVersion

    defaultConfig {
        applicationId "com.sunfusheng.gank"
        minSdkVersion gradle.minSdkVersion
        targetSdkVersion gradle.targetSdkVersion
        versionCode gradle.versionCode
        versionName gradle.versionName
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "6g"
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }

    signingConfigs {
        release {
            storeFile file(signingProperties['KEYSTORE_FILEPATH'])
            storePassword signingProperties['STORE_PASSWORD']
            keyAlias signingProperties['KEY_ALIAS']
            keyPassword signingProperties['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "debugMode", "false"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateApk(variant)
            }
        }

        debug {
            buildConfigField "boolean", "debugMode", "true"
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateApk(variant)
            }
        }
    }
}

def generateApk(variant) {
    if (variant.buildType.name == "release") {
        variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/app/outputs")
        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
            apkData.outputFileName = "Gank.IO.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:' + gradle.supportLibraryVersion
    implementation 'com.android.support:design:' + gradle.supportLibraryVersion
    implementation 'com.android.support:recyclerview-v7:' + gradle.supportLibraryVersion
    implementation 'com.android.support:support-v4:' + gradle.supportLibraryVersion
    implementation 'com.android.support:support-v13:' + gradle.supportLibraryVersion

    // Network
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // Rx2
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'zlc.season:rxdownload2:1.1.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    // Others
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.orhanobut:logger:2.1.1'
    implementation 'me.drakeet.multitype:multitype:3.4.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.4'
    implementation 'com.sunfusheng:glideimageview:1.2.0'
    compile 'com.sunfusheng:FirUpdater:1.0.7'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
